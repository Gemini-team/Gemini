#pragma require 2darray

#include "UnityCG.cginc"
#include "UnityShaderVariables.cginc"

UNITY_DECLARE_TEX2DARRAY(depthImages);

float4x4 CameraRotationMatrices[31];
float4x4 inv_CameraMatrix;
float4x4 CameraMatrix;

uint ImageWidthRes;
uint ImageHeightRes;
int NrOfImages;

// https://stackoverflow.com/questions/32227283/getting-world-position-from-depth-buffer-value
float3 ImageToCartesian(uint2 TexCoord, uint CameraId) {

	//index by UVZ
	float3 UVWCoord = float3(TexCoord.x/float(ImageWidthRes), TexCoord.y/float(ImageHeightRes), CameraId);
	// need explicit mipmap level of 0 here, as d3d11 tries to to some gradient stuff without it
	float depth = UNITY_SAMPLE_TEX2DARRAY_LOD(depthImages, UVWCoord, 0).x; 

	float W = float(ImageWidthRes);
	float H = float(ImageHeightRes);

	// from Unity image frame to OpenGL clip space
	float x = 2 * TexCoord.x / W - 1;
	float y = 2 * TexCoord.y / H - 1;
	float z = (1 - depth) * 2.0 - 1.0;

	// force dead points (no objects within far plane) to have reflections at near plane
	if ((1 - depth) == 1) {
		z = -1; 
	}

	float4 clipSpacePosition = float4(x, y, z, 1.0);
	float4 viewSpacePosition = mul(inv_CameraMatrix, clipSpacePosition); // inverted projection matrix (as provided by openGL, right handed)
	// see bottom of first answer of https://answers.unity.com/questions/1359718/what-do-the-values-in-the-matrix4x4-for-cameraproj.html

	// Perspective division
	viewSpacePosition /= viewSpacePosition.w;
	viewSpacePosition.z *= -1; // convert from right handed [right, up, backward] to left handed [right, up, forward]

	float4 bodySpacePosition = mul(CameraRotationMatrices[CameraId], viewSpacePosition); // rotation matrix provided by Unity (in left handed land) 

	return bodySpacePosition.xyz;
}
